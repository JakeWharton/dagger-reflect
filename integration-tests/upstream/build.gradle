apply plugin: 'java-library'

sourceSets.test.java {
  srcDir file('dagger/javatests')
  include 'dagger/functional/**'

  exclude 'dagger/functional/aot/**'
  exclude 'dagger/functional/producers/**'
  exclude 'dagger/functional/tck/**'
  exclude 'dagger/functional/spi/**'

  // These tests contains a reference to a generated types directly.
  exclude 'dagger/functional/gwt/GwtIncompatiblesTest.java'
  exclude 'dagger/functional/membersinject/MembersInjectTest.java'

  // This test reflects on the generated component implementation fields.
  exclude 'dagger/functional/staticprovides/StaticProvidesTest.java'

  // TODO reflect-compiler bug! Not generating builder method.
  exclude 'dagger/functional/builder/BuilderTest.java'
  exclude 'dagger/functional/cycle/DoubleCheckCycleTest.java'
  exclude 'dagger/functional/membersinject/MembersInjectionOrderingTest.java'
  exclude 'dagger/functional/nullables/NullabilityTest.java'
  exclude 'dagger/functional/MultibindingTest.java'
  exclude 'dagger/functional/NonComponentDependencyTest.java'
}

test.filter {
  // TODO reflect bug! Not supporting @Reusable scope.
  excludeTest 'dagger.functional.binds.BindsTest', null
  excludeTest 'dagger.functional.ReusableTest', null

  // TODO reflect bug! JIT binding depending on JIT binding is inexplicably not working.
  excludeTest 'dagger.functional.cycle.CycleTest', null
  excludeTest 'dagger.functional.cycle.LongCycleTest', 'longCycle'
  excludeTest 'dagger.functional.DependsOnGeneratedCodeTest', 'testComponentDependsOnGeneratedCode'
  excludeTest 'dagger.functional.NestedTest', 'nestedFoo'

  // TODO reflect bug! Need something like ByteBuddy for proxying classes at runtime.
  excludeTest 'dagger.functional.builderbinds.BuilderBindsTest', null
  excludeTest 'dagger.functional.subcomponent.SubcomponentTest', null
  excludeTest 'dagger.functional.subcomponent.repeat.RepeatedModuleTest', null
  excludeTest 'dagger.functional.BasicTest', null

  // TODO reflect bug! Components can have multiple scope annotations.
  excludeTest 'dagger.functional.scope.ScopeTest', null

  // TODO reflect bug! Generics don't work well.
  excludeTest 'dagger.functional.GenericTest', null

  // TODO reflect bug! Module subcomponents do not work.
  excludeTest 'dagger.functional.subcomponent.pruning.SubcomponentOnlyRequestedBySiblingTest', 'subcomponentAddedInParent_onlyUsedInSibling'
  excludeTest 'dagger.functional.subcomponent.ModuleWithSubcomponentsTest', null

  // TODO reflect bug! Injecting Provides<T> does not work.
  excludeTest 'dagger.functional.cycle.CycleTest', 'cycleWithDeferredBinds'
  excludeTest 'dagger.functional.cycle.CycleTest', 'providerMapIndirectionCycle'
  excludeTest 'dagger.functional.guava.OptionalBindingComponentsAbsentTest', null
  excludeTest 'dagger.functional.guava.OptionalBindingComponentsPresentTest', null
  excludeTest 'dagger.functional.jdk8.OptionalBindingComponentsEmptyTest', null
  excludeTest 'dagger.functional.jdk8.OptionalBindingComponentsPresentTest', null
  excludeTest 'dagger.functional.ComponentMethodTest', null

  // TODO reflect bug! Injecting Lazy<T> does not work.
  excludeTest 'dagger.functional.LazyMapsTest', null

  // TODO reflect bug! Subcomponent factory provision method does not work.
  excludeTest 'dagger.functional.subcomponent.SubcomponentFactoryMethodTest', null

  // TODO reflect bug! Members injection on JIT binding does not work.
  excludeTest 'dagger.functional.cycle.CycleTest', 'lazyIndirectionSelfCycle'
  excludeTest 'dagger.functional.cycle.CycleTest', 'providerIndirectionSelfCycle'
  excludeTest 'dagger.functional.NestedTest', 'nestedBar'

  // TODO reflect bug! Subcomponent multibindings propagation does not work.
  excludeTest 'dagger.functional.subcomponent.SubcomponentMultibindingsTest', null

  // Uses @MapKey which does not have runtime retention.
  excludeTest 'dagger.functional.multibindings.ComplexMapKeysInDifferentOrderTest', 'test'
  excludeTest 'dagger.functional.multibindings.MapKeyWithDefaultTest', 'test'
}

dependencies {
  testImplementation project(':reflect')
  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.auto.value.annotations
  testImplementation deps.auto.factory // TODO https://github.com/google/auto/issues/631
  testAnnotationProcessor project(':reflect-compiler')
  testAnnotationProcessor deps.auto.value.compiler
  testAnnotationProcessor deps.auto.factory
}

def ensureDaggerSubmodule = tasks.create('ensureDaggerSubmodule') {
  doFirst {
    if (!file('dagger/.git').exists()) {
      throw new RuntimeException(
          "Missing 'dagger' git submodule clone. Did you run 'git submodule update --init'?")
    }
    def describe = 'git describe'.execute(null, file('dagger'))
    if (describe.waitFor() != 0) {
      System.err.println(describe.errorStream.text)
      throw new RuntimeException("Could not run 'git describe' in 'dagger' git submodule clone")
    }
    if ("dagger-${versions.dagger}" != describe.inputStream.text.trim()) {
      throw new RuntimeException(
          "Wrong tag checked out for 'dagger' git submodule clone. Should be 'dagger-${versions.dagger}'.")
    }
  }
}
compileTestJava.dependsOn(ensureDaggerSubmodule)
