import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  ext.versions = [
      'dagger': 'HEAD-SNAPSHOT', // TODO update to 2.22 or newer once released
      'incap' : '0.2'
  ]

  ext.deps = [
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
          'androidRuntime': "com.google.dagger:dagger-android:${versions.dagger}",
          'androidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      'junit': 'junit:junit:4.13-beta-1',
      'truth': 'com.google.truth.extensions:truth-java8-extension:0.43',
      'compileTesting': 'com.google.testing.compile:compile-testing:0.15',
      'javaPoet': 'com.squareup:javapoet:1.11.1',
      'auto': [
          'service': [
              'annotations': 'com.google.auto.service:auto-service-annotations:1.0-rc5',
              'compiler': 'com.google.auto.service:auto-service:1.0-rc5',
          ],
          'factory': 'com.google.auto.factory:auto-factory:1.0-beta6',
          'value': [
              'annotations': 'com.google.auto.value:auto-value-annotations:1.6.3',
              'compiler': 'com.google.auto.value:auto-value:1.6.3',
          ],
      ],
      'jetbrainsAnnotations': 'org.jetbrains:annotations:17.0.0',
      'guava': 'com.google.guava:guava:27.0.1-jre',
      'animalSnifferAnnotations': 'org.codehaus.mojo:animal-sniffer-annotations:1.17',
      'robolectric': 'org.robolectric:robolectric:4.3-alpha-1',
      'androidx': [
          'test': [
              'core': 'androidx.test:core:1.1.0',
          ],
      ],
      'incap': [
          'runtime': "net.ltgt.gradle.incap:incap:${versions.incap}",
          'processor': "net.ltgt.gradle.incap:incap-processor:${versions.incap}",
      ]
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.7.1'
    classpath 'net.ltgt.gradle:gradle-nullaway-plugin:0.1'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
    classpath 'com.android.tools.build:gradle:3.3.2'
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'net.ltgt.errorprone'
  apply plugin: 'net.ltgt.nullaway'

  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
    maven {
      url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }

  dependencies {
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'
    errorprone 'com.uber.nullaway:nullaway:0.6.6'
  }

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      disableWarningsInGeneratedCode = true
      excludedPaths = ".*/build/generated/.*"

      check("JavaxInjectOnAbstractMethod", CheckSeverity.OFF)

      nullaway {
        annotatedPackages.add('dagger.reflect')
        severity = CheckSeverity.ERROR
      }
    }
  }
}
