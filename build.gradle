import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  ext.versions = [
      'dagger': '2.23.2',
      'incap' : '0.2'
  ]

  ext.deps = [
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
          'androidRuntime': "com.google.dagger:dagger-android:${versions.dagger}",
          'androidRuntimeJar': "com.google.dagger:dagger-android-jarimpl:${versions.dagger}",
          'androidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      'junit': 'junit:junit:4.13-beta-3',
      'truth': 'com.google.truth.extensions:truth-java8-extension:0.45',
      'compileTesting': 'com.google.testing.compile:compile-testing:0.18',
      'javaPoet': 'com.squareup:javapoet:1.11.1',
      'auto': [
          'service': [
              'annotations': 'com.google.auto.service:auto-service-annotations:1.0-rc5',
              'compiler': 'com.google.auto.service:auto-service:1.0-rc5',
          ],
          'factory': 'com.google.auto.factory:auto-factory:1.0-beta6',
          'value': [
              'annotations': 'com.google.auto.value:auto-value-annotations:1.6.5',
              'compiler': 'com.google.auto.value:auto-value:1.6.5',
          ],
      ],
      'jetbrainsAnnotations': 'org.jetbrains:annotations:17.0.0',
      'guava': 'com.google.guava:guava:28.0-jre',
      'animalSnifferAnnotations': 'org.codehaus.mojo:animal-sniffer-annotations:1.18',
      'robolectric': 'org.robolectric:robolectric:4.3',
      'androidx': [
          'test': [
              'core': 'androidx.test:core:1.2.0',
          ],
      ],
      'incap': [
          'runtime': "net.ltgt.gradle.incap:incap:${versions.incap}",
          'processor': "net.ltgt.gradle.incap:incap-processor:${versions.incap}",
      ]
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.8.1'
    classpath 'net.ltgt.gradle:gradle-nullaway-plugin:0.2'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.5.0'
    classpath 'com.android.tools.build:gradle:3.4.1'
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'net.ltgt.errorprone'
  apply plugin: 'net.ltgt.nullaway'

  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }

  dependencies {
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    errorprone 'com.google.errorprone:error_prone_core:2.3.3'
    errorprone 'com.uber.nullaway:nullaway:0.7.3'
  }

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      disableWarningsInGeneratedCode = true
      excludedPaths = ".*/build/generated/.*"

      // TODO https://github.com/tbroyer/gradle-nullaway-plugin/issues/2
      option('NullAway:AssertsEnabled', 'true')

      nullaway {
        annotatedPackages.add('dagger.reflect')
        severity = CheckSeverity.ERROR
      }
    }
  }
}
