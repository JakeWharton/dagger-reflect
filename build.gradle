import net.ltgt.gradle.errorprone.CheckSeverity

buildscript {
  ext.versions = [
      'dagger': '2.21',
  ]

  ext.deps = [
      'dagger': [
          'runtime': "com.google.dagger:dagger:${versions.dagger}",
          'compiler': "com.google.dagger:dagger-compiler:${versions.dagger}",
          'androidRuntime': "com.google.dagger:dagger-android:${versions.dagger}",
          'androidCompiler': "com.google.dagger:dagger-android-processor:${versions.dagger}",
      ],
      'junit': 'junit:junit:4.13-beta-1',
      'truth': 'com.google.truth:truth:0.42',
      'compileTesting': 'com.google.testing.compile:compile-testing:0.15',
      'javaPoet': 'com.squareup:javapoet:1.11.1',
      'auto': [
          'service': 'com.google.auto.service:auto-service:1.0-rc4',
          'value': [
              'annotations': 'com.google.auto.value:auto-value-annotations:1.6.3',
              'compiler': 'com.google.auto.value:auto-value:1.6.3',
          ],
      ],
      'jetbrainsAnnotations': 'org.jetbrains:annotations:16.0.3',
      'guava': 'com.google.guava:guava:27.0.1-jre',
      'animalSnifferAnnotations': 'org.codehaus.mojo:animal-sniffer-annotations:1.16',
      'robolectric': 'org.robolectric:robolectric:4.2',
      'androidx': [
          'test': [
              'core': 'androidx.test:core:1.1.0',
          ],
      ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.7'
    classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.6'
    classpath 'com.android.tools.build:gradle:3.3.1'
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'net.ltgt.errorprone'

  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }

  tasks.withType(Test) {
    testLogging {
      events "skipped", "failed", "passed"
    }
  }

  dependencies {
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
    errorprone 'com.google.errorprone:error_prone_core:2.3.2'
    errorprone 'com.uber.nullaway:nullaway:0.6.5'
  }

  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      disableWarningsInGeneratedCode = true
      excludedPaths = ".*/build/generated/.*"
      check("JavaxInjectOnAbstractMethod", CheckSeverity.OFF)
      check("NullAway", CheckSeverity.ERROR)
      option("NullAway:AnnotatedPackages", "dagger.reflect")
    }
  }
}
